apply plugin: 'maven'
apply plugin: 'signing'


group = 'com.yandex.android'
version = '1.0'

ext.isTeamcityBuild = project.hasProperty('teamcity')
ext.isMavenCentralBuild = project.hasProperty('mavenCentral')

project.afterEvaluate {

    uploadArchives {
        repositories {
            mavenDeployer {

                pom.project {
                    parent {
                        groupId 'org.sonatype.oss'
                        artifactId 'oss-parent'
                        version '7'
                    }
                    modelVersion '4.0.0'
                    groupId 'com.yandex.android'
                    artifactId 'ym-android-sdk'
                    version version
                    name 'Yandex.Money SDK for Android'
                    description 'This Android library provides tools for performing payments using Yandex.Money public API on devices running Android 4.0 (API 14) or higher. The library uses Yandex.Money SDK for Java.'
                    url 'https://tech.yandex.ru/money/'

                    licenses {
                        license {
                            name 'Agreement on Use of the Yandex.Money API'
                            url 'https://money.yandex.ru/doc.xml?id=526828&ncrnd=1189/'
                            distribution 'repo'
                        }
                    }

                    scm {
                        connection 'scm:git://github.com/yandex-money/yandex-money-sdk-android.git'
                        developerConnection 'scm:git://github.com/yandex-money/yandex-money-sdk-android.git'
                        url 'https://github.com/yandex-money/yandex-money-sdk-android.git'
                    }

                    developers {
                        developer {
                            name 'Yandex.Money'
                            url 'https://money.yandex.ru/'
                            roles {
                                role 'Developer'
                                role 'Contributor'
                            }
                        }
                    }
                }.writeTo("build/outputs/ym-cps-android-sdk-" + version + ".pom")

                if (isTeamcityBuild || isMavenCentralBuild) {
                    if (isMavenCentralBuild) {
                        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
                    }
                    project.ext['auth'] = { authentication(userName: teamcity['maven.username'], password: teamcity['maven.password']) }
                    repository(url: teamcity['maven.repository'], project.ext['auth'])
                    snapshotRepository(url: teamcity['maven.snapshotRepository'], project.ext['auth'])
                } else {
                    repository(url: "file://${System.env.HOME}/.yandex-money-sdk-repo/")
                }
            }
        }
    }
}
